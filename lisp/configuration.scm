(define-class (configuration)
  ;; Configuration class stores nested config data as a single expression.

  (define* (*init* self (config '()))
    ;; Initialize configuration with optional expression.
    ;;   Args:
    ;;     config (list/expression): configuration expression.
    ;;   Returns:
    ;;     boolean: #t after mutation.
    (set! (self '(1)) (expression->byte-vector config)))

  (define (get self path)
    ;; Get value at nested path.
    ;;   Args:
    ;;     path (list of symbols): path segments.
    ;;   Returns:
    ;;     any: value or '() when missing.
    (let loop ((config (byte-vector->expression (self '(1)))) (path path))
      (if (null? path) config
          (let ((match (assoc (car path) config)))
            (if (not match) '()
                (loop (cadr match) (cdr path)))))))

  (define (set! self path value)
    ;; Set or delete value at nested path (value '() deletes).
    ;;   Args:
    ;;     path (list of symbols): path segments.
    ;;     value (any): value to set.
    ;;   Returns:
    ;;     boolean: #t after mutation.
    (set! (self '(1))
          (expression->byte-vector
           (let loop-1 ((config (byte-vector->expression (self '(1)))) (path path))
             (if (null? path) value
                 (let loop-2 ((config config))
                   (cond ((null? config)
                          (if (eq? value '()) '()
                              (list (list (car path) (loop-1 '() (cdr path))))))
                         ((eq? (caar config) (car path))
                          (let ((result (loop-1 (cadar config) (cdr path))))
                            (if (eq? result '()) (cdr config)
                                (cons (list (car path) result) (cdr config)))))
                         (else (cons (car config) (loop-2 (cdr config))))))))))))
